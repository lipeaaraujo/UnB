algoritmo conteudoTexto;
// Síntese
//  Objetivo:  Retornar informações sobre um texto.
//  Entrada : Um texto.
//  Saída   : As informações com o conteúdo do texto.

principal
	// Declarações
	
	texto txt;
	caracter leitor;
	inteiro i, qntdCarac, qntdMaiuscula, qntdMinuscula, qntdNumero, qntdSimbolo;
	logico maiuscula, minuscula, numero, simbolo;
	
	// Instruções

	// Entrada de dados:
	leia(txt);

	// Verificação de conteúdo:

	//Quantidade de caracteres:
	qntdCarac = tamanhoTexto(txt);

	qntdMaiuscula = 0;
	qntdMinuscula = 0;
	qntdNumero = 0;
	qntdSimbolo = 0;
	
	para (i de 0 ate (tamanhoTexto(txt) - 1) passo 1) faca
		leitor = (caracterTexto(txt, i));
		
		//Letra maiúscula:
		maiuscula = (caracterAscii(leitor) >= 65 e caracterAscii(leitor) <= 90);
		se (maiuscula == verdadeiro) entao
			qntdMaiuscula = qntdMaiuscula + 1;
		fimSe

		//Letra minúscula:
		minuscula = (caracterAscii(leitor) >= 97 e caracterAscii(leitor) <= 122);
		se (minuscula == verdadeiro) entao
			qntdMinuscula = qntdMinuscula + 1;
		fimSe

		//Número:
		numero = (caracterAscii(leitor) >= 48 e caracterAscii(leitor) <= 57);
		se (numero == verdadeiro) entao
			qntdNumero = qntdNumero + 1;
		fimSe

		//Símbolo:
		se (nao maiuscula e nao minuscula e nao numero) entao
			qntdSimbolo = qntdSimbolo + 1;
		fimSe
	fimPara

	escreval("QUANTIDADE DE CARACTERES = ", qntdCarac);
	escreval("QUANTIDADE DE MAIUSCULAS = ", qntdMaiuscula);
	escreval("QUANTIDADE DE MINUSCULAS = ", qntdMinuscula);
	escreval("QUANTIDADE DE NUMEROS = ", qntdNumero);
	escreval("QUANTIDADE DE SIMBOLOS = ", qntdSimbolo);
	
fimPrincipal
