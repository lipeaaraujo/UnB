algoritmo codificadorMensagem; 

// Síntese 
//  Objetivo: Codificar ou descodificar uma mensagem inserida pelo usuário.
//  Entrada : 	O modo (codificação ou descodificação), e a mensagem.
//  Saída   : 	A mensagem de acordo com o modo utilizado.

principal 
	// Declarações 

	caracter modoCod, caracCod; 
	texto mensagem; 
	inteiro i;
	logico letra, numero, simbolo;

	// Instruções 

	//Entrada de dados 

	//escreva("Modo: "); 
	leiaCaracter(modoCod); 
	//escreva("Mensagem: "); 
	leia(mensagem); 

	modoCod = minusculoCaracter(modoCod); 
	i = 0; 
	
	/* 
	Letra: caracterAscii(caracCod) >= 65 e caracterAscii(caracCod) <= 90 ou caracterAscii(caracCod) >= 97 e caracterAscii(caracCod) <= 122 
	Número: caracterAscii(caracCod) >= 48 e caracterAscii(caracCod) <= 57 
	Símbolo: qualquer outro valor Ascii 
	*/ 

	escolha (modoCod) 
		caso 'c' 
			//Codifica o texto. 
			enquanto (i < tamanhoTexto(mensagem)) faca 
				caracCod = caracterTexto(mensagem, i);
				
				letra = caracterAscii(caracCod) >= 65 e caracterAscii(caracCod) <= 90 ou caracterAscii(caracCod) >= 97 e caracterAscii(caracCod) <= 122;
				numero = caracterAscii(caracCod) >= 48 e caracterAscii(caracCod) <= 57;
				
				se (letra == verdadeiro) entao 
					//Caso for letra: 
					caracCod = asciiCaracter((caracterAscii(caracCod)) - 20); 
					escreva(caracCod); 
				senao 
					se (numero == verdadeiro) entao 
						//Caso for número: 
						caracCod = asciiCaracter((caracterAscii(caracCod)) - 15); 
						escreva(caracCod); 
					senao 
						//Caso for símbolo: 
						escreva("|", caracCod); 
					fimSe 
				fimSe 
				i = i + 1; 
			fimEnquanto 
			interrompa;
		caso 'd' 
			//Descodifica o texto. 
			enquanto (i < tamanhoTexto(mensagem)) faca 
				caracCod = caracterTexto(mensagem, i); 

				//Variáveis lógicas que verificam o tipo de caracter
				letra = caracterAscii(caracCod) >= 45 e caracterAscii(caracCod) <= 70 ou caracterAscii(caracCod) >= 77 e caracterAscii(caracCod) <= 102;
				numero = caracterAscii(caracCod) >= 33 e caracterAscii(caracCod) <= 42;
				
				se (caracCod == '|') entao
					//Essa primeira condição verifica se o caracter após "|" é um símbolo ou não
					
					simbolo = caracterAscii(caracterTexto(mensagem, i + 1)) < 65 e caracterAscii(caracterTexto(mensagem, i + 1)) > 90
					ou caracterAscii(caracterTexto(mensagem, i + 1)) < 97 ou caracterAscii(caracterTexto(mensagem, i + 1)) > 122
					ou caracterAscii(caracterTexto(mensagem, i + 1)) < 48 ou caracterAscii(caracterTexto(mensagem, i + 1)) > 57;
					
					se (simbolo) entao
						escreva(caracterTexto(mensagem, i + 1));
						i = i + 1;
					fimSe
				senao
					se (letra == verdadeiro) entao 
						//Caso for letra: 
						caracCod = asciiCaracter((caracterAscii(caracCod)) + 20); 
						escreva(caracCod); 
					senao 
						se (numero == verdadeiro) entao 
							//Caso for número: 
							caracCod = asciiCaracter((caracterAscii(caracCod)) + 15); 
							escreva(caracCod);  
						fimSe 
					fimSe
				fimSe
				i = i + 1;
			fimEnquanto			 
			interrompa;
	fimEscolha 
	
	escreval(""); 

fimPrincipal
