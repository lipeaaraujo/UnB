algoritmo equacoesPontos;
// Síntese
//  Objetivo:  Calcular a equação de uma reta, circunferência ou parábola a partir de certos pontos inseridos pelo usuário.
//  Entrada : Indicação do tipo de equação a ser calculada e as coordenadas x e y dos pontos de referência.
//  Saída   : A equação específica e opcionalmente a(s) sua(s) raíz(es).


principal

	// Declarações

	inteiro i, j;
	inteiro tipo, instrucao;
	real coordX[2], coordY[2];
	texto equacao;
	logico pontosIguais;
	
	// Instruções

	informacoes_iniciais();
	
	// Entrada de dados

	faca
		escreval("Qual tipo de equação deseja calcular?");
		escreval("1 - Equação da reta");
		escreval("2 - Equação da circunferência");
		escreval("3 - Equação da parábola");
		escreval("");
		leia(tipo);

		limpaTela();
		
		faca
			escolha(tipo)
			caso 1
				// Equação da reta
				escreval("Insira os dois pontos com suas coordenadas no plano cartesiano:");
				para(i de 0 ate 1 passo 1) faca
					escreva("x", i+1 + " = ");
					leia(coordX[i]);
					escreva("y", i+1 + " = ");
					leia(coordY[i]);
				fimPara
				limpaTela();
				interrompa;
			caso 2
				// Equação da circunferência
				escreval("Insira um ponto e suas coordenadas no plano cartesiano:");
				i = 0;
				escreva("x", i+1 + " = ");
				leia(coordX[i]);
				escreva("y", i+1 + " = ");
				leia(coordY[i]);
					
				escreval("Insira o ponto central da circunferência::");
				escreva("Xc = ");
				leia(coordX[1]);
				escreva("Yc = ");
				leia(coordY[1]);
				limpaTela();
				interrompa;
			caso 3
				// Equação da parábola
				escreval("Insira os dois pontos e suas coordenadas no plano cartesiano:");
				para(i de 0 ate 1 passo 1) faca
					escreva("x", i+1 + " = ");
					leia(coordX[i]);
					escreva("y", i+1 + " = ");
					leia(coordY[i]);
				fimPara
				limpaTela();
				interrompa;
			fimEscolha
	
			// Verifica se os pontos são válidos
			pontosIguais = falso;
			se(tipo == 1 ou tipo == 3) entao
				se(coordX[0] == coordX[1]) entao
					pontosIguais = verdadeiro;
				fimSe
			senao
				se(coordX[0] == coordX[1] e coordY[0] == coordY[1]) entao
					pontosIguais = verdadeiro;
				fimSe
			fimSe
			

			// Chama o procedimento
			se(pontosIguais == falso) entao
				escolha(tipo)
				caso 1
					escreval("Equação da reta nos pontos ");
					para (i de 0 ate 1 passo 1) faca
						escreva("(" + coordX[i] + ", " + coordY[i] + ") ");
					fimPara
					escreval("");
					calculaEquacao(coordX, coordY, tipo);
					interrompa;
				caso 2
					escreval("Equação da circunferência no ponto ");
					i = 0;
					escreva("(" + coordX[i] + ", " + coordY[i] + ") ");
					escreva(" e centro (" + coordX[1] + ", " + coordY[1] + ") ");
					escreval("");
					calculaEquacao(coordX, coordY, tipo);
					interrompa;
				caso 3
					escreval("Equação da parábola nos pontos ");
					para (i de 0 ate 1 passo 1) faca
						escreva("(" + coordX[i] + ", " + coordY[i] + ") ");
					fimPara
					escreval("");
					calculaEquacao(coordX, coordY, tipo);
					interrompa;
				fimEscolha
			senao
				escreval("Pontos inválidos! Reescreva as coordenadas!");
				escreval("");
			fimSe
		enquanto (pontosIguais == verdadeiro);
		
		// Receber instrução do usuário
		escreval("");
		escreval("1 - Continuar programa");
		escreval("2 - Encerrar programa");
		escreval("");
		leia(instrucao);
		escreval("");
		limpaTela();
		
	enquanto (instrucao != 2);
fimPrincipal

procedimento calculaEquacao(real coordX[], real coordY[], inteiro tipo)
	real a, b, c;
	texto equacao;
	inteiro instrucao;

	c = 0;
	escolha(tipo)
		caso 1
		 	// Calcula a equação da reta:
			// (y = ax + b)
			// a = (y - y0)/(x - x0) | b = y - (ax)
			a = (coordY[1] - coordY[0]) / (coordX[1] - coordX[0]);
			b = coordY[0] - (coordX[0] * a);

			// Saída
			escreval("");
			se (b >= 0) entao
				escreval("y = ", a::2, "x + ", b::2);
			senao
				escreval("y = ", a::2, "x ", b::2);
			fimSe
			interrompa;
		caso 2
			// Calcula a equação da circunferência:
			// (x - a)² + (y - b)² = R²
			a = coordX[1];
			b = coordY[1];
			c = raizQuadrada(exp((coordX[0] - coordX[1]), 2) + exp((coordY[0] - coordY[1]), 2));

			// Saída
			se(a >= 0 e b >= 0) entao
				escreval("(x-", a::2, ")² + (y-", b::2, ")² = ", c::2, "²");
			senao
				se(b >= 0) entao
					escreval("(x+", -a::2, ")² + (y-", b::2, ")² = ", c::2, "²");
				senao
					se(a >= 0) entao
						escreval("(x-", a::2, ")² + (y+", -b::2, ")² = ", c::2, "²");	
					senao
						escreval("(x+", -a::2, ")² + (y+", -b::2, ")² = ", c::2, "²");
					fimSe
				fimSe		
			fimSe
			interrompa;
		caso 3
			// Calcula a equação da parábola:
			// (y = x² + bx + c)
			// b = ((y1 + y2) - (x1² - x2²)) / (x1 - x2) | c = (y - x² - bx)
			b = (exp(coordX[1], 2) + coordY[0] - exp(coordX[0], 2) - coordY[1]) / (coordX[0] - coordX[1]);
			c = (coordY[0] - exp(coordX[0], 2) - (b * coordX[0]));

			// Saída
			se(b >= 0 e c >= 0) entao
				escreval("y = x²+", b::2, "x+", c::2);
			senao
				se(b >= 0) entao
					escreval("y = x²+", b::2, "x", c::2);
				senao
					se(c >= 0) entao
						escreval("y = x²", b::2, "x+", c::2);
					senao
						escreval("y = x²", b::2, "x", c::2);
					fimSe
				fimSe
			fimSe
			interrompa;
	fimEscolha

	// Instrução do usuário
	escreval("");
	escreval("Deseja ver as raízes?");
	escreval("1 - Sim	2 - Não");
	escreval("");
	leia(instrucao);

	se(instrucao == 1) entao
		calculaRaiz(a, b, c, tipo);
	fimSe
fimProcedimento

procedimento calculaRaiz(real a, real b, real c, inteiro tipo)
	// Calcula as raízes da equação
	// (y = 0)
	real raiz[2];
	real delta;

	escreval("");
	escolha(tipo)
	caso 1
		// Equação de reta
		// (y = ax + b)

		se (a != 0) entao
			raiz[0] = (- b) / a;
		senao
			escreval("Essa equação não possui raíz real!");
		fimSe
		
		se(raiz[0] != 0) entao
			escreval("Raiz: x = ", raiz[0]::2);
		senao
			escreval("Raiz: x = 0");
		fimSe
		interrompa;
	caso 2
		// Equação da circunferência
		// (x - a)² + (y - b)² = R²

		delta = (exp((-2 * a), 2) - (4 * ((exp(a, 2) + exp(b, 2) - exp(c, 2)))));
		se(delta >= 0) entao
			raiz[0] = ((2 * a) + raizQuadrada(delta)) / 2;
			raiz[1] = ((2 * a) - raizQuadrada(delta)) / 2;
	
			escreval("Raiz: x1 = ", raiz[0]::2);
			escreval("Raiz: x2 = ", raiz[1]::2);
		senao
			escreval("Essa equação não possui raízes reais!");
		fimSe
		interrompa;
	caso 3
		// Equação da parábola
		// (y = x² + bx + c)

		delta = exp(b, 2) - (4 * c);
		se(delta > 0) entao
			raiz[0] = (-b + raizQuadrada(delta)) / 2;
			raiz[1] = (-b - raizQuadrada(delta)) / 2;
	
			escreval("Raiz: x1 = ", raiz[0]::2);
			escreval("Raiz: x2 = ", raiz[1]::2);
		senao
			escreval("Essa equação não possui raízes reais!");
		fimSe
		interrompa;
	fimEscolha

fimProcedimento

procedimento informacoes_iniciais()
	escreval("*************************************************************************");
	escreval("                   - EQUAÇÕES A PARTIR DE PONTOS -");
	escreval("         Autor: Felipe Amorim       FGA - UNB   APC T02   2022.1");
	escreval("*************************************************************************");
fimProcedimento
